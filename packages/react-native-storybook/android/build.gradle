def DEFAULT_COMPILE_SDK_VERSION = 30
def DEFAULT_TARGET_SDK_VERSION = 30

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'
apply plugin: "com.facebook.react"

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }
    
    buildFeatures {
        prefab true
    }
}

// Set up the react-native configuration for code generation
if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../")
        libraryName = "sherlomodulespec"
        codegenJavaPackageName = "io.sherlo.storybookreactnative"
    }
    
    // Include the generated sources directory
    android {
        sourceSets {
            main {
                java {
                    srcDirs += [
                        project.file("$buildDir/generated/source/codegen/java")
                    ]
                }
            }
        }
    }
}

repositories{
    mavenCentral()
    google()
}

dependencies {
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    
    if (isNewArchitectureEnabled()) {
        implementation project(':ReactAndroid')
        implementation "com.facebook.fbjni:fbjni:0.2.2"
        implementation "com.facebook.react:react-native-gradle-plugin"
    }
}